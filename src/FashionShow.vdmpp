class FashionShow

/*

	Defines a fashion show, it's the main class of the project.
  J. Oliveira, FEUP, MFES, 2017/18.

*/


instance variables 
	public name : FSUtils`string;
  public place : FSUtils`string;
  public startDate : FSUtils`date;
  public endDate : FSUtils`date;
  public events : seq of (Event);
  public peopleAttending : set of (Person);

-- invariants
inv FSUtils`isOldestDate(startDate, endDate);
inv not exists e1, e2 in set elems events & (e1 <> e2 and e1.place = e2.place and FSUtils`coincDate(e1.startDate,e1.endDate, e2.startDate, e2.endDate));

operations
-- constructor
	public FashionShow : FSUtils`string * FSUtils`string * FSUtils`date * FSUtils`date ==> FashionShow
		FashionShow(n, p, sD, eD) == (
			name := n;
			place := p;
			startDate := sD;
			endDate := eD;
			events := [];
			peopleAttending := {};
			return self;		
		)
		post events = [] and peopleAttending = {};
		
-- set name
 	public setName : FSUtils`string ==> ()
 		setName(n) ==
 			name := n;
 			
-- set place
 	public setPlace : FSUtils`string ==> ()
 		setPlace(p) ==
 			place := p;
			
-- set start date
	public setStartDate : FSUtils`date ==> ()
		setStartDate(d) ==
			startDate := d;
			
-- set end date
	public setEndDate : FSUtils`date ==> ()
		setEndDate(d) ==
			endDate := d;
			
-- set events
	public setEvents : seq of (Event) ==> ()
		setEvents(e) ==
			events := e;
			
-- add event
	public addEvent : Event ==> ()
		addEvent(e) ==
			events := events ^ [e]
			post events = events~ ^ [e];
			
-- add person to people attending event
	public addPersonAttending : Person ==> ()
		addPersonAttending(p) ==
			peopleAttending := peopleAttending union {p}
			pre p not in set peopleAttending
			post peopleAttending = peopleAttending~ union {p};
			
-- get designers attending the event			
	public getDesigners : () ==> set of (Person)
		getDesigners() == (
			dcl ret : set of Person := {};
			for all p in set peopleAttending do(
				if p.isDesigner then ret := ret union {p};
			);
			return ret;
		);

end FashionShow
